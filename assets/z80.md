# A PEG grammar for Z80 assembly language

## Start

```
// Start (top level symbol) of the grammar
start := Lines $ // Les followed by the end of the code
```

## Values

```
// Names of the registers for the different opcodes (the names are from the Z80 CPU User Manual)
r := 'a' | 'b' | 'c' | 'd' | 'e' | 'h' | 'l'
dd := 'bc' | 'de' | 'hl' | 'sp'
ss := dd
qq := 'bc' | 'de' | 'hl' | 'af'
pp := 'bc' | 'de' | 'ix' | 'sp'
rr := 'bc' | 'de' | 'iy' | 'sp'

// 8-bit values
// Note: binary is before hexadecimal because 0b is ambiguous
n :=
    binary | hexadecimal | octal | decimal

// 16-bit values
// Note: binary is before hexadecimal because 0b is ambiguous
nn :=
    binary | hexadecimal | octal | decimal

// A decimal unsigned value with the different allowed syntaxes
decimal :=
    '[0-9]+' 'd'?

// A binary value with the different allowed syntaxes
binary :=
    {'0b' | '%'} '[0-1]+'

// An octal value with the different allowed syntaxes
octal :=
    {'0q' | '0o'} '[0-7]+' |
    '[0-7]+' {'q' | 'o'}

// An hexadecimal value with the different allowed syntaxes
hexadecimal :=
    {'0x' | '\$' | '#'} '[0-9a-f]+' |
    '[0-9][0-9a-f]?' 'h'

// Different names used in the Z80 CPU User Manual for 8-bit values
d := n
e := n
p := n

// A bit value
b := '[0-7]'

// Conditional flags for jumps
cc := 'nz' | 'z' | 'nc' | 'c' | 'po' | 'pe' | 'p' | 'm' | 's'
// Conditional flags for djnz
jj := 'c'  | 'nc' | 'z' | 'nz'
```

## Spaces

```
__   := '[ \t]+'                 // Mandatory space
_    := '[ \t]*'                 // Optional space
_eos := '[ \t]*(;|\r\n|\n|//)'   // End of statement

```

## Lines

```
// Lines is a list of lines (can be empty)
Lines :=
    Line*

// A line is either an equality or a statement
Line :=
  LineEqual | LineStatement

// An equality is a label (mandatory) followed by an equality directive, an optional comment and an end of line.
LineEqual :=
  LabelDeclaration _ EqualDirective _ Comment? '\r\n|\n'

// A line with a statement is an optional label, followed by a statement, an optional comment and an end of line.
LineStatement :=
  LabelDeclaration? _ Statement? _ Comment? '\r\n|\n'

```

## Statements

```
// A statement is either in include, and instruction or directive
Statement :=
    IncludeDirective | Instruction | Directive

// An equality directive is 'equ' (or an alias) followed by an expression
EqualDirective :=
    '\.?equ|eq' __ Expression

// A directive is either a data directive, an origin directive, an output directive or a device directive
Directive :=
    DataDirective | OriginDirective | OutputDirective | DeviceDirective

```

## Labels

```
// Names not allowed for a label
ForbiddenNames :=
    'block|.\?equ|\.?org|\.?include|\.?output|\.?device' |
    'byte|call|cpdr|cpir|defb|defm|defs|defw|djnz|eq|halt|indr|inir|lddr|ldir|otdr|otir|outd|outi|push|reti|retn|rlca|rrca|word' |
    'adc|add|and|bit|ccf|cpd|cpi|cpl|daa|dec|exx|inc|ind|ini|ldd|ldi|neg|nop|org|out|pop|res|ret|rla' |
    'rlc|rld|rra|rrc|rrd|rst|sbc|scf|set|sla|sra|srl|sub|xor' |
    'cp|db|di|dm|ds|dw|dz|ei|eq|ex|im|in|jp|jr|ld|or|rl|rr'

// A forbidden label is a name not allowed for a label followed by a space, a tabulation, the character ':' or an end of line.
ForbiddenLabel :=
    ForbiddenNames '[ \t:\r\n]'

// A label can't have a forbidden name. It is a name.
Label :=
    !ForbiddenLabel '[a-z_][a-z0-9_!\?#@\.]*'

// A label declaration can't have a forbidden name. It is a name optionally followed by a ':' character.
LabelDeclaration :=
    !ForbiddenLabel '[a-z_][a-z0-9_!\?#@\.]*' '\:'?
```

## Expressions

```
// Note: Top level expressions can't start with a parenthesis, otherwise it is ambiguous with dereferences
// like LD HL, (0)

// An expression can't start with an open parenthesis. it is a bitwise OR expression
Expression :=
    !'\(' BitwiseOrExpression

// Precedences: unary, multiplicative, additive, shift, and, xor, or
// Associativity: left to right

// A bitwise OR expression is a list of bitwise OR expressions (eventually empty) followed by a bitwise XOR expression
BitwiseOrExpression :=
    {BitwiseOrExpression _ '\|'}* _ BitwiseXOrExpression

// A bitwise XOR expression is a list of bitwise XOR expressions (eventually empty) followed by a bitwise AND expression
BitwiseXOrExpression :=
    {BitwiseXOrExpression _ '\^'}* _ BitwiseAndExpression

// A bitwise AND expression is a list of bitwise AND expressions (eventually empty) followed by a bitwise Shift expression
BitwiseAndExpression :=
    {BitwiseAndExpression _ '\&'}* _ ShiftExpression

// A bitwise Shift expression is a list of bitwise Shift expressions (eventually empty) followed by an Additive expression
ShiftExpression :=
    {ShiftExpression _ {'<<' | '>>'}}* _ AdditiveExpression

// An Additive expression is a list of Additive expressions (eventually empty) followed by a Multiplicative expression
AdditiveExpression :=
    {AdditiveExpression _ {'\+' | '\-'}}* _ MultiplicativeExpression

// A Multiplicative expression is a list of Multiplicative expressions (eventually empty) followed by an Unary expression
MultiplicativeExpression :=
    {MultiplicativeExpression _ {'\*' | '%' | '/'}}* _ UnaryExpression

// An Unary expression is an optional unary operator followed by a Primary expression
UnaryExpression :=
    {'~' | '\+' | '\-'} _ PrimaryExpression |
    PrimaryExpression

// A Primary expression is a either a number, a label or an expression enclosed in parenthesis
PrimaryExpression :=
    nn |
    Label |
    '\(' BitwiseOrExpression '\)'

// A 8-bit register is the name of a 8-bit register but can't be followed by characters such as alphanumerical
// or symbols. This is to distinguish registers from labels
Reg8 :=
    r &'[^a-z0-9_!\?#@\.]'
```

## Z80 Instructions

```
// An instruction
Instruction :=
    Load8Instruction |
    Load16Instruction |
    ExchangeInstruction |
    BlockInstruction |
    ArithmeticLogic8Instruction |
    IncrementDecrement8Instruction |
    GeneralPurposeArithmeticInstruction |
    ArithmeticLogic16Instruction |
    RotateShiftInstruction |
    BitManipulationInstruction |
    JumpInstruction |
    CallInstruction |
    ReturnInstruction |
    InputInstruction |
    OutputInstruction |
    CpuControlGroupInstruction
```

### 8-bit Load

```
Load8Instruction :=
    // LD r, r'
    'ld' __ Reg8 _ ',' _ Reg8 |

    // LD r, (HL)
    'ld' __ r _ ',' _ '\(' 'hl' '\)' |

    // LD r, (IX+d)
    'ld' __ r _ ',' _ '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)' |

    // LD r, (IY+d)
    'ld' __ r _ ',' _ '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)' |

    // LD (HL), r
    'ld' __ '\(' _ 'hl' _ '\)' _ ',' _ r |

    // LD (IX+d), r
    'ld' __ '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)' _ ',' _ r |

    // LD (IY+d), r
    'ld' __ '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)' _ ',' _ r |

    // LD (HL), n
    'ld' __ '\(' 'hl' '\)' _ ',' _ Expression |

    // LD (IX+d), n
    'ld' __ '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)' _ ',' _ Expression |

    // LD (IY+d), n
    'ld' __ '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)' _ ',' _ Expression |

    // LD A, (BC)
    'ld' __ 'a' _ ',' _ '\(' _ 'bc' _ '\)' |

    // LD A, (DE)
    'ld' __ 'a' _ ',' _ '\(' _ 'de' _ '\)' |

    // LD A, (nn)
    'ld' __ 'a' _ ',' _ '\(' _ Expression _ '\)' |

    // LD (BC), A
    'ld' __ '\(' _ 'bc' _ '\)' _ ',' _ 'a' |

    // LD (DE), A
    'ld' __ '\(' _ 'de' _ '\)' _ ',' _ 'a' |

    // LD (nn), A
    'ld' __ '\(' _ Expression _ '\)' _ ',' _ 'a' |

    // LD A, I
    'ld' __ 'a' _ ',' _ 'i' |

    // LD A, R
    'ld' __ 'a' _ ',' _ 'r' |

    // LD I, A
    'ld' __ 'i' _ ',' _ 'a' |

    // LD R, A
    'ld' __ 'r' _ ',' _ 'a' |

    // Have to be after the others to avoid ambiguity on Expression
    // LD r, n
    // Have to be before LD r,r' because it is ambiguous
    'ld' __ Reg8 _ ',' _ Expression
```

### 16-bit Load

```
Load16Instruction :=
    // LD IX, nn
    'ld' __ 'ix' _ ',' _ Expression |

    // LD IY, nn
    'ld' __ 'iy' _ ',' _ Expression |

    // LD HL, (nn)
    'ld' __ 'hl' _ ',' _ '\(' _ Expression _ '\)' |

    // LD dd, (nn)
    'ld' __ dd _ ',' _ '\(' _ Expression _ '\)' |

    // LD IX, (nn)
    'ld' __ 'ix' _ ',' _ '\(' _ Expression _ '\)' |

    // LD IY, (nn)
    'ld' __ 'iy' _ ',' _ '\(' _ Expression _ '\)' |

    // LD (nn), HL
    'ld' __ '\(' _ Expression _ '\)' _ ',' _ 'hl' |

    // LD (nn), dd
    'ld' __ '\(' _ Expression _ '\)' _ ',' _ dd |

    // LD (nn), IX
    'ld' __ '\(' _ Expression _ '\)' _ ',' _ 'ix' |

    // LD (nn), IY
    'ld' __ '\(' _ Expression _ '\)' _ ',' _ 'iy' |

    // LD SP, HL
    'ld' __ 'sp' _ ',' _ 'hl' |

    // LD SP, IX
    'ld' __ 'sp' _ ',' _ 'ix' |

    // LD SP, IY
    'ld' __ 'sp' _ ',' _ 'iy' |

    // Have to be after the other LD instructions
    // LD dd, nn
    'ld' __ dd _ ',' _ Expression |

    // PUSH qq
    'push' __ qq |

    // PUSH IX
    'push' __ 'ix' |

    // PUSH IY
    'push' __ 'iy' |

    // POP qq
    'pop' __ qq |

    // POP IX
    'pop' __ 'ix' |

    // POP IY
    'pop' __ 'iy'
```

### Exchange

```
ExchangeInstruction :=
    // EX DE, HL
    'ex' __ 'de' _ ',' _ 'hl'  |

    // EX AF, AF'
    'ex' __ 'af' _ ',' _ 'af\'' |

    // EXX
    'exx' |

    // EX (SP), HL
    'ex' __ '\(' 'sp' '\)' _ ',' _ 'hl' |

    // EX (SP), IX
    'ex' __ '\(' 'sp' '\)' _ ',' _ 'ix' |

    // EX (SP), IY
    'ex' __ '\(' 'sp' '\)' _ ',' _ 'iy'

```

### Block Transfer and Search

```
BlockInstruction :=
    // Have to be before ldi
    // LDIR
    'ldir' |

    // LDI
    'ldi' |

    // LDDR
    'lddr' |

    // LDD
    'ldd' |

    // CPIR
    'cpir' |

    // CPI
    'cpi' |

    // CPDR
    'cpdr' |

    // CPD
    'cpd'

```

### 8-bit Arithmetic

```
ArithmeticLogic8Instruction :=
    // ADD A, r
    'add' __ {'a' _ ',' _}? r |

    // ADD A, n
    'add' __ {'a' _ ',' _}? Expression |

    // ADD A, (HL)
    'add' __ {'a' _ ',' _}? '\(' _ 'hl'_ '\)' |

    // ADD A, (IX+d)
    'add' __ {'a' _ ',' _}? '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)' |

    // ADD A, (IY+d)
    'add' __ {'a' _ ',' _}? '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)' |

    // ADC A, r
    'adc' __ {'a' _ ',' _}? r |

    // ADC A, n
    'adc' __ {'a' _ ',' _}? Expression |

    // ADC A, (HL)
    'adc' __ {'a' _ ',' _}? '\(' _ 'hl'_ '\)' |

    // ADC (IX+d)
    'adc' __ {'a' _ ',' _}? '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)' |

    // ADC (IY+d)
    'adc' __ {'a' _ ',' _}? '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)' |

    // SUB A, r
    'sub' __ {'a' _ ',' _}? r |

    // SUB A, n
    'sub' __ {'a' _ ',' _}? Expression |

    // SUB A, (HL)
    'sub' __ {'a' _ ',' _}? '\(' _ 'hl'_ '\)' |

    // SUB (IX+d)
    'sub' __ {'a' _ ',' _}? '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)' |

    // SUB (IY+d)
    'sub' __ {'a' _ ',' _}? '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)' |

    // SBC A, r
    'sbc' __ {'a' _ ',' _}? r |

    // SBC A, n
    'sbc' __ {'a' _ ',' _}? Expression |

    // SBC A, (HL)
    'sbc' __ {'a' _ ',' _}? '\(' _ 'hl'_ '\)' |

    // SBC (IX+d)
    'sbc' __ {'a' _ ',' _}? '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)' |

    // SBC (IY+d)
    'sbc' __ {'a' _ ',' _}? '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)' |

    // AND A, r
    'and' __ {'a' _ ',' _}? r |

    // AND A, n
    'and' __ {'a' _ ',' _}? Expression |

    // AND A, (HL)
    'and' __ {'a' _ ',' _}? '\(' _ 'hl'_ '\)' |

    // AND (IX+d)
    'and' __ {'a' _ ',' _}? '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)' |

    // AND (IY+d)
    'and' __ {'a' _ ',' _}? '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)' |

    // OR A, r
    'or' __ {'a' _ ',' _}? r |

    // OR A, n
    'or' __ {'a' _ ',' _}? Expression |

    // OR A, (HL)
    'or' __ {'a' _ ',' _}? '\(' _ 'hl'_ '\)' |

    // OR (IX+d)
    'or' __ {'a' _ ',' _}? '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)' |

    // OR (IY+d)
    'or' __ {'a' _ ',' _}? '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)' |

    // XOR A, r
    'xor' __ {'a' _ ',' _}? r |

    // XOR A, n
    'xor' __ {'a' _ ',' _}? Expression |

    // XOR A, (HL)
    'xor' __ {'a' _ ',' _}? '\(' _ 'hl'_ '\)' |

    // XOR (IX+d)
    'xor' __ {'a' _ ',' _}? '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)' |

    // XOR (IY+d)
    'xor' __ {'a' _ ',' _}? '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)' |

    // CP A, r
    'cp' __ {'a' _ ',' _}? r |

    // CP A, n
    'cp' __ {'a' _ ',' _}? Expression |

    // CP A, (HL)
    'cp' __ {'a' _ ',' _}? '\(' _ 'hl'_ '\)' |

    // CP (IX+d)
    'cp' __ {'a' _ ',' _}? '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)' |

    // CP (IY+d)
    'cp' __ {'a' _ ',' _}? '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)'
```

### 8-bit Increment and Decrement

```
IncrementDecrement8Instruction :=
    // INC r
    'inc' __ r |

    // INC (HL)
    'inc' __ '\(' _ 'hl' _ '\)' |

    // INC (IX+d)
    'inc' __ '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)' |

    // INC (IY+d)
    'inc' __ '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)' |

    // DEC r
    'dec' __ r |

    // DEC (HL)
    'dec' __ '\(' _ 'hl' _ '\)' |

    // DEC (IX+d)
    'dec' __ '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)' |

    // DEC (IY+d)
    'dec' __ '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)'
```

### General Purpose Arithmetic Instructions

```
GeneralPurposeArithmeticInstruction :=
    // DAA
    'daa' |

    // CPL
    'cpl' |

    // NEG
    'neg' |

    // CCF
    'ccf' |

    // SCF
    'scf'
```

# CPU Control Group Instructions

```
CpuControlGroupInstruction :=
    // NOP
    'nop' |

    // HALT
    'halt' |

    // DI
    'di' |

    // EI
    'ei' |

    // IM 0, IM 1, IM 2
    'im' __ '[012]'
```

# 16-bit Arithmetic

```
ArithmeticLogic16Instruction :=
    // ADD HL, ss
    'add' __ 'hl' _ ',' _ ss |

    // ADC HL, ss
    'adc' __ 'hl' _ ',' _ ss |

    // SBC HL, ss
    'sbc' __ 'hl' _ ',' _ ss |

    // ADD IX, pp
    'add' __ 'ix' _ ',' _ pp |

    // ADD IY, pp
    'add' __ 'iy' _ ',' _ rr |

    // INC ss
    'inc' __ ss |

    // INC IX
    'inc' __ 'ix' |

    // INC IY
    'inc' __ 'iy' |

    // DEC ss
    'dec' __ ss |

    // DEC IX
    'dec' __ 'ix' |

    // DEC IY
    'dec' __ 'iy'
```

### Rotate and Shift

```
RotateShiftInstruction :=
    // RLCA
    'rlca' |

    // RLA
    'rla' |

    // RRCA
    'rrca' |

    // RRA
    'rra' |

    // RLC r
    'rlc' __ r |

    // RLC (HL)
    'rlc' __ '\(' _ 'hl' _ '\)' |

    // RLC (IX+d)
    'rlc' __ '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)' |

    // RLC (IY+d)
    'rlc' __ '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)' |

    // RL r
    'rl' __ r |

    // RL (HL)
    'rl' __ '\(' _ 'hl' _ '\)' |

    // RL (IX+d)
    'rl' __ '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)' |

    // RL (IY+d)
    'rl' __ '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)' |

    // RRC r
    'rrc' __ r  |

    // RRC (HL)
    'rrc' __ '\(' _ 'hl' _ '\)' |

    // RRC (IX+d)
    'rrc' __ '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)' |

    // RRC (IY+d)
    'rrc' __ '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)' |

    // RR r
    'rr' __ r |

    // RR (HL)
    'rr' __ '\(' _ 'hl' _ '\)' |

    // RR (IX+d)
    'rr' __ '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)' |

    // RR (IY+d)
    'rr' __ '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)' |

    // SLA r
    'sla' __ r |

    // SLA (HL)
    'sla' __ '\(' _ 'hl' _ '\)' |

    // SLA (IX+d)
    'sla' __ '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)' |

    // SLA (IY+d)
    'sla' __ '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)' |

    // SRA r
    'sra' __ r |

    // SRA (HL)
    'sra' __ '\(' _ 'hl' _ '\)' |

    // SRA (IX+d)
    'sra' __ '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)' |

    // SRA (IY+d)
    'sra' __ '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)'  |

    // SRL r
    'srl' __ r |

    // SRL (HL)
    'srl' __ '\(' _ 'hl' _ '\)' |

    // SRL (IX+d)
    'srl' __ '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)' |

    // SRL (IY+d)
    'srl' __ '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)'  |

    // RLD
    'rld' |

    // RRD
    'rrd'
```

### Bit Manipulation

```
BitManipulationInstruction :=
    // BIT b, r
    'bit' __ b _ ',' _ r |

    // BIT b, (HL)
    'bit' __ b _ ',' _ '\(' _ 'hl' _ '\)' |

    // BIT b, (IX+d)
    'bit' __ b _ ',' _ '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)'  |

    // BIT b, (IY+d)
    'bit' __ b _ ',' _ '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)' |

    // SET b, r
    'set' __ b _ ',' _ r |

    // SET b, (HL)
    'set' __ b _ ',' _ '\(' _ 'hl' _ '\)' |

    // SET b, (IX+d)
    'set' __ b _ ',' _ '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)'  |

    // SET b, (IY+d)
    'set' __ b _ ',' _ '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)' |

    // RES b, r
    'res' __ b _ ',' _ r|

    // RES b, (HL)
    'res' __ b _ ',' _ '\(' _ 'hl' _ '\)'|

    // RES b, (IX+d)
    'res' __ b _ ',' _ '\(' _ 'ix' _ {'\+' | '\-'} _ Expression _ '\)' |

    // RES b, (IY+d)
    'res' __ b _ ',' _ '\(' _ 'iy' _ {'\+' | '\-'} _ Expression _ '\)'
```

### Jumps

```
// Jump offset
Offset :=
    Label | Expression

// Jumps
JumpInstruction :=
    // JP nn
    'jp' __ Expression |

    // JP CC, nn
    'jp' __ cc _ ',' _ Expression  |

    // JR label
    'jr' __ Label |

    // JR e
    'jr' __ Expression |

    // JR jj, label
    'jr' __ jj _ ',' _ Label  |

    // JR jj, e
    'jr' __ jj _ ',' _ Expression  |

    // JP (HL)
    'jp' __ '\(' _ 'hl' _ '\)' |

    // JP (IX)
    'jp' __ '\(' _ 'ix' _ '\)' |

    // JP (IY)
    'jp' __ '\(' _ 'iy' _ '\)' |

    // DJNZ label
    'djnz' __ Label |

    // DJNZ e
    'djnz' __ Expression
```

### Calls

```
CallInstruction :=
    // CALL nn
    'call' __ Expression |

    // CALL cc, nn
    'call' __ cc _ ',' _ Expression
```

### Returns

```
ReturnInstruction :=
    // RET
    'ret' |

    // RET cc
    'ret' __ cc |

    // RETI
    'reti' |

    // RETN
    'retn' |

    // RST p
    'rst' __ p
```

### Inputs

```
InputInstruction :=
    // Have to be before IN A, (n)
    // IN r, (C)
    'in' __ r _ ',' _ '\(' 'c' '\)' |

    // IN A, (n)
    'in' __ 'a' _ ',' _ '\(' _ Expression _ '\)' |

    // INI
    'ini' |

    // INIR
    'inir' |

    // IND
    'ind' |

    // INDR
    'indr'
```

### Outputs

```
OutputInstruction :=
    // Have to be before OUT (n), A
    // OUT (C), r
    'out' __ '\(' 'c' '\)' _ ',' _ r |

    // OUT (n), A
    'out' __ '\(' _ Expression _ '\)' _ ',' _ 'a' |

    // OUTI
    'outi' |

    // OTIR
    'otir' |

    // OUTD
    'outd' |

    // OTDR
    'otdr'
```

## Data Directives

```
// Byte directive
ByteDirective :=
    'db' | 'dm' | 'byte' | 'defb' | 'defm'

// Word directive
WordDirective :=
    'dw' | 'word' | 'defw'

// Block directive
BlockDirective :=
    'block' | 'defs' | 'ds'

// A Byte directive contains either characters, strings, ZX81 strings or expressions
ByteValue :=
    Char | Str | ZX81Str | Expression

// A Word directive contains expressions
WordValue :=
    Expression

// A Data directive is either a Byte directive, a Word directive or a Block directive
DataDirective :=
    // byte $FF, ...
    ByteDirective __ ByteValue {_ ',' _ ByteValue}* |

    // word $FFFF, ...
    WordDirective __ WordValue {_ ',' _ WordValue}* |

    // block length, fill byte
    BlockDirective __ Expression {_ ',' _ Expression}?
```

## Characters and strings

```
// Escapes taken from C++ (C++ standard 20, A.2 Lexical conventions) with some modifications

// An escape sequence is either simple, in octal or in hexadecimal
EscapeSequence :=
    SimpleEscapeSequence | OctalEscapeSequence | HexadecimalEscapeSequence

// A simple escape is a backslash followed by a character
SimpleEscapeSequence :=
    '\\' {'\'' | '"' | '\?' | '\\' | 'a' | 'b' | 'e' | 'f' | 'n' | 't' | 'r' | 'v'}

// An octal escape is a backslash followed by an octal value
OctalEscapeSequence :=
    '\\' '[0-7]{1,3}'

// An hexadecimal escape is a backslash followed by an 'x' and an hexadecimal value
HexadecimalEscapeSequence :=
    '\\x' '[0-9a-f]{1,2}'

// A simple character is not a quote, a backslash or a newline
SimpleChar :=
    '[^\'\\\n]'

// A string character is not a double-quote, a backslash or a newline
StringChar :=
    '[^\"\\\n]'

// A character literal is enclosed in quotes and is either a simple character or an escape sequence
Char :=
    '\'' {SimpleChar | EscapeSequence} '\''

// A string literal is enclosed in double-quotes and contains either simple characters or escape sequences
Str :=
    '\"' {StringChar | EscapeSequence}* '\"'

// A ZX81 string literal starts with 'zx81', is enclosed in double-quotes and contains only characters common to
// ASCII and the ZX81 characters set.
ZX81Str :=
    'zx81\"' '[ Â£\$:\?()><=+\-\*;/,.0-9A-Za-z]*' '\"'
```

## Comments

```
// A comment starts with '//' or ';' and goes until the end of the statement
Comment :=
    '(//|;)[^\r\n]*'
```

## Other Directives


```
// An Origin directive is 'org' (or an alias) followed by a 16-bit value and the end of the statement
OriginDirective :=
    '\.?org' __ nn

// A filename is optionally enclosed in double-quotes and contains a subset of characters.
Filename :=
    '"' '[^"\\:\*\?<>\|%#\$,]+' '"' |
    '[^ \t\r\n"\\:\*\?<>\|%#\$,]+'

// An Include directive starts with 'include' (or an alias) and is followed by a Filename
IncludeDirective :=
    '\.?include' __ Filename

// An Output directive starts with 'output' (or an alias) and is followed by a Filename.
// Optionally, it is followed by a comma, 'sld' and an filename
OutputDirective :=
    '\.?output' __ Filename {_ ',' _ 'sld' __ Filename}?

// A Device directive starts with 'device' (or an alias) and is followed by a name (the target)
DeviceDirective :=
    '\.?device' __ '[a-zA-Z][0-9a-zA-Z]*'
```
